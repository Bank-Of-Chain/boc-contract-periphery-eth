// SPDX-License-Identifier: MIT

/*
    This file is part of the Enzyme Protocol.
    (c) Enzyme Council <council@enzyme.finance>
    For the full license information, please view the LICENSE
    file that was distributed with this source code.
*/

pragma solidity >=0.8.0 <0.9.0;

import '../../external/uniswap/IUniswapV2Router2.sol';
import '../AssetHelpers.sol';

/// @title UniswapV2ActionsMixin Contract
/// @author Enzyme Council <security@enzyme.finance>
/// @notice Mixin contract for interacting with Uniswap v2
abstract contract UniswapV2ActionsMixin is AssetHelpers {

    address internal constant UNISWAP_V2_ROUTER2 = address(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);

    /// @dev Helper to execute a swap
    function __uniswapV2Swap(
        address _recipient,
        uint256 _outgoingAssetAmount,
        uint256 _minIncomingAssetAmount,
        address[] memory _path
    ) internal {
        __approveAssetMaxAsNeeded(_path[0], UNISWAP_V2_ROUTER2, _outgoingAssetAmount);

        // Execute fill
        IUniswapV2Router2(UNISWAP_V2_ROUTER2).swapExactTokensForTokens(
            _outgoingAssetAmount,
            _minIncomingAssetAmount,
            _path,
            _recipient,
            __uniswapV2GetActionDeadline()
        );
    }

    /// @dev Helper to swap many assets to a single target asset.
    /// The intermediary asset will generally be WETH, and though we could make it
    // per-outgoing asset, seems like overkill until there is a need.
    function __uniswapV2SwapManyToOne(
        address _recipient,
        address[] memory _outgoingAssets,
        uint256[] memory _outgoingAssetAmounts,
        address _incomingAsset,
        address _intermediaryAsset
    ) internal {
        bool _noIntermediary = _intermediaryAsset == address(0) ||
        _intermediaryAsset == _incomingAsset;
        for (uint256 i=0; i < _outgoingAssets.length; i++) {
            // Skip cases where outgoing and incoming assets are the same, or
            // there is no specified outgoing asset or amount
            if (
                _outgoingAssetAmounts[i] == 0 ||
                _outgoingAssets[i] == address(0) ||
                _outgoingAssets[i] == _incomingAsset
            ) {
                continue;
            }

            address[] memory _uniswapPath;
            if (_noIntermediary || _outgoingAssets[i] == _intermediaryAsset) {
                _uniswapPath = new address[](2);
                _uniswapPath[0] = _outgoingAssets[i];
                _uniswapPath[1] = _incomingAsset;
            } else {
                _uniswapPath = new address[](3);
                _uniswapPath[0] = _outgoingAssets[i];
                _uniswapPath[1] = _intermediaryAsset;
                _uniswapPath[2] = _incomingAsset;
            }

            __uniswapV2Swap(_recipient, _outgoingAssetAmounts[i], 1, _uniswapPath);
        }
    }

    /// @dev Helper to get the deadline for a Uniswap V2 action in a standardized way
    function __uniswapV2GetActionDeadline() private view returns (uint256 _deadline) {
        return block.timestamp + 1;
    }

    ///////////////////
    // STATE GETTERS //
    ///////////////////

    /// @notice Gets the `UNISWAP_V2_ROUTER2` variable
    /// @return _router The `UNISWAP_V2_ROUTER2` variable value
    function getUniswapV2Router2() public pure returns (address _router) {
        return UNISWAP_V2_ROUTER2;
    }
}
